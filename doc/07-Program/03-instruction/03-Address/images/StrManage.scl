FUNCTION_BLOCK "StrManage"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      str : String[8];   // 源输入字符串
   END_VAR

   VAR_OUTPUT 
      dst : DWord;   // 输出双字
   END_VAR

   VAR_TEMP 
      tempStr : DInt;   // 源字符串下标
      tempArray : Array[0..7] of Char;   // 中间字符数组
      tempA : Int;   // 中间字符数组下标
      tempRet : Word;   // ATH 输出代码
   END_VAR


BEGIN
	//清空中间字符数组
	FILL_BLK(IN := '$00',
	         COUNT := 8,
	         OUT => #tempArray[0]);
	//对中间字符数组下标清零
	#tempA := 0;
	
	//对字符串字符 1 - 8 循环检查 
	FOR #tempStr := 1 TO 8 DO
	    //如果字符在 0-9、a-f、A-F，则送到中间字符数组，并且中间字符数组下标自加一
	    IF (#str[#tempStr] >= '0' & #str[#tempStr] <= '9')
	        OR (#str[#tempStr] >= 'a' & #str[#tempStr] <= 'f')
	        OR (#str[#tempStr] >= 'A' & #str[#tempStr] <= 'F')
	    THEN
	        #tempArray[#tempA] := #str[#tempStr];
	        #tempA += 1;
	        //否则什么都不做
	    ELSE
	        CONTINUE;
	    END_IF;
	END_FOR;
	
	//最后中间字符数组下标就是总的有效字符个数
	//使用 ATH 指令进行字符到十六进制的转换
	#tempRet:=ATH(IN := #tempArray, N := #tempA, OUT => #dst);
	
	
END_FUNCTION_BLOCK

